/*******************************************************************************
    Copyright 2008 Sun Microsystems, Inc.,
    4150 Network Circle, Santa Clara, California 95054, U.S.A.
    All rights reserved.

    U.S. Government Rights - Commercial software.
    Government users are subject to the Sun Microsystems, Inc. standard
    license agreement and applicable provisions of the FAR and its supplements.

    Use is subject to license terms.

    This distribution may include materials developed by third parties.

    Sun, Sun Microsystems, the Sun logo and Java are trademarks or registered
    trademarks of Sun Microsystems, Inc. in the U.S. and other countries.
 ******************************************************************************/

/*
 * Definition of Fortress abstract field declarations.
 */
module com.sun.fortress.parser.AbsField(Variable, Header, NoNewlineType,
                                        Identifier, Spacing);

modify Variable;
import Header;
import NoNewlineType;
import Identifier;
import Spacing;

/* AbsFldDecl ::=
     AbsFldMods? NoNewlineFldWTypes
   | AbsFldMods? BindIdOrBindIdTuple w : w NoNewlineType w ...
   | AbsFldMods? BindIdOrBindIdTuple w : w NoNewlineTupleType
 */
VarDecl AbsFldDecl = AbsVarDecl ;

Modifiers AbsVarMods := AbsFldMods ;

/* ApiFldDecl ::=
     ApiFldMods? NoNewlineFldWTypes
   | ApiFldMods? BindIdOrBindIdTuple w : w NoNewlineType w ...
   | ApiFldMods? BindIdOrBindIdTuple w : w NoNewlineTupleType
 */
VarDecl ApiFldDecl =
     a1:ApiFldMods? a2:NoNewlineVarWTypes
     { if (a1 != null) a2 = FortressUtil.setMods(a2, a1);
       FortressUtil.validId(a2);
       yyValue = new VarDecl(createSpan(yyStart,yyCount), a2, Option.<Expr>none());
     }
   / a1:ApiFldMods? a2:BindIdOrBindIdTuple w colon w a3:NoNewlineType w ellipses
     { List<LValue> lvs;
       if (a1 != null) lvs = FortressUtil.ids2Lvs(a2, a1, a3, false);
       else            lvs = FortressUtil.ids2Lvs(a2, a3, false);
       FortressUtil.validId(lvs);
       yyValue = new VarDecl(createSpan(yyStart,yyCount), lvs, Option.<Expr>none());
     }
   / a1:ApiFldMods? a2:BindIdOrBindIdTuple w colon w a3:NoNewlineTupleType
     { Span span = createSpan(yyStart,yyCount);
       List<Type> tys = a3.getElements();
       if (a2.size() != tys.size())
           syntaxError(span, "Mismatched numbers of variables and types.");
       List<LValue> lvs;
       if (a1 != null) lvs = FortressUtil.ids2Lvs(a2, a1, tys, false);
       else            lvs = FortressUtil.ids2Lvs(a2, tys, false);
       FortressUtil.validId(lvs);
       yyValue = new VarDecl(span, lvs, Option.<Expr>none());
     };
