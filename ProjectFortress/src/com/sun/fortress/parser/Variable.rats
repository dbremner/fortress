/*******************************************************************************
    Copyright 2009 Sun Microsystems, Inc.,
    4150 Network Circle, Santa Clara, California 95054, U.S.A.
    All rights reserved.

    U.S. Government Rights - Commercial software.
    Government users are subject to the Sun Microsystems, Inc. standard
    license agreement and applicable provisions of the FAR and its supplements.

    Use is subject to license terms.

    This distribution may include materials developed by third parties.

    Sun, Sun Microsystems, the Sun logo and Java are trademarks or registered
    trademarks of Sun Microsystems, Inc. in the U.S. and other countries.
 ******************************************************************************/

/*
 * Definition of Fortress variable declarations.
 */
module com.sun.fortress.parser.Variable(NoNewlineHeader, MayNewlineHeader,
                                        NoNewlineType, NoNewlineExpr,
                                        Identifier, Keyword, Symbol, Spacing);

import NoNewlineHeader;
import MayNewlineHeader;
import NoNewlineType;
import NoNewlineExpr;
import Identifier;
import Keyword;
import Symbol;
import Spacing;

/* VarDecl ::=
     Mods? VarMayTypes w InitVal
   | Mods? BindIdOrBindIdTuple w = w NoNewlineExpr
   | Mods? BindIdOrBindIdTuple w : w NoNewlineType w ... w InitVal
   | Mods? BindIdOrBindIdTuple w : w NoNewlineTupleType w InitVal
 */
VarDecl VarDecl =
     a1:Mods? a2:VarMayTypes w a3:InitVal
     { if (a1 != null) {
           if (a3.getB()) {
               FortressUtil.allHaveTypes(writer, a2);
               a2 = FortressUtil.setModsAndMutable(a2, a1);
           } else {
               if (a1.isMutable())
                   FortressUtil.allHaveTypes(writer, a2);
               a2 = FortressUtil.setMods(a2, a1);
           }
       } else {
             if (a3.getB()) {
                 FortressUtil.allHaveTypes(writer, a2);
                 a2 = FortressUtil.setMutable(a2);
             }
       }
       FortressUtil.validId(writer, a2);
       yyValue = NodeFactory.makeVarDecl(writer, createSpan(yyStart,yyCount), a2,
                                         Option.<Expr>some(a3.getA()));
     }
   / a1:Mods? a2:BindIdOrBindIdTuple w equals w a3:NoNewlineExpr
     { Span span = createSpan(yyStart,yyCount);
       List<LValue> lvs;
       if (a1 != null) {
           if ( a1.isMutable() )
               log(span, "Mutable variables should be declared with their types.");
           lvs = FortressUtil.ids2Lvs(a2, a1);
       } else lvs = FortressUtil.ids2Lvs(a2);
       FortressUtil.validId(writer, lvs);
       yyValue = NodeFactory.makeVarDecl(writer, span, lvs,
                                         Option.<Expr>some(a3));
     }
   / a1:Mods? a2:BindIdOrBindIdTuple w colon w a3:NoNewlineType w ellipses w a4:InitVal
     { List<LValue> lvs;
       if (a1 != null) {
           if (a4.getB()) lvs = FortressUtil.ids2Lvs(a2, a1, a3, true);
           else           lvs = FortressUtil.ids2Lvs(a2, a1, a3, false);
       } else {
           if (a4.getB()) lvs = FortressUtil.ids2Lvs(a2, a3, true);
           else           lvs = FortressUtil.ids2Lvs(a2, a3, false);
       }
       FortressUtil.validId(writer, lvs);
       yyValue = NodeFactory.makeVarDecl(writer, createSpan(yyStart,yyCount), lvs,
                                         Option.<Expr>some(a4.getA()));
     }
   / a1:Mods? a2:BindIdOrBindIdTuple w colon w a3:NoNewlineTupleType w a4:InitVal
     { Span span = createSpan(yyStart,yyCount);
       List<Type> tys = a3.getElements();
       if (a2.size() != tys.size())
           log(span, "Mismatched numbers of variables and types.");
       List<LValue> lvs;
       if (a1 != null) {
           if (a4.getB()) lvs = FortressUtil.ids2Lvs(a2, a1, tys, true);
           else           lvs = FortressUtil.ids2Lvs(a2, a1, tys, false);
       } else {
           if (a4.getB()) lvs = FortressUtil.ids2Lvs(a2, tys, true);
           else           lvs = FortressUtil.ids2Lvs(a2, tys, false);
       }
       FortressUtil.validId(writer, lvs);
       yyValue = NodeFactory.makeVarDecl(writer, span, lvs,
                                         Option.<Expr>some(a4.getA()));
     }
   / <ErrorProduction>
     var w BindIdOrBindIdTuple w equals w NoNewlineExpr
     { Span span = createSpan(yyStart,yyCount);
       log(span, "Mutable variables should be declared with their types.");
       yyValue = NodeFactory.makeVarDecl(writer, span,
                                         Collections.<LValue>emptyList(),
                                         Option.<Expr>none());
     };

/* VarMayTypes ::=
     VarMayType
   | ( w VarMayType (w , w VarMayType)+ w )
 */
List<LValue> VarMayTypes =
     a1:VarMayType
     { yyValue = FortressUtil.mkList(a1); }
   / openparen w a1:VarMayType a2s:(w comma w VarMayType)+ w closeparen
     { yyValue = FortressUtil.mkList(a1, a2s.list()); };

/* VarMayType ::= BindId (w IsType)? */
LValue VarMayType = a1:BindId a2:(w IsType)
     { if (a2 == null)
           yyValue = NodeFactory.makeLValue(createSpan(yyStart,yyCount),a1);
       else
           yyValue = NodeFactory.makeLValue(createSpan(yyStart,yyCount),a1,a2);
     };

/* InitVal ::= (=|:=) w NoNewlineExpr */
com.sun.fortress.useful.Pair<Expr,Boolean> InitVal =
     a1:(equals / colonequals) w a2:NoNewlineExpr
     { boolean isMutable = a1.equals(":=") || a1.equals("\u2254");
       yyValue = new com.sun.fortress.useful.Pair<Expr,Boolean>(a2, isMutable);
     };

/* AbsVarDecl ::=
     Mods? NoNewlineVarWTypes
   | Mods? BindIdOrBindIdTuple w : w NoNewlineType w ...
   | Mods? BindIdOrBindIdTuple w : w NoNewlineTupleType
 */
VarDecl AbsVarDecl =
     a1:Mods? a2:NoNewlineVarWTypes
     { if (a1 != null) a2 = FortressUtil.setMods(a2, a1);
       FortressUtil.validId(writer, a2);
       yyValue = NodeFactory.makeVarDecl(writer, createSpan(yyStart,yyCount), a2,
                                         Option.<Expr>none());
     }
   / a1:Mods? a2:BindIdOrBindIdTuple w colon w a3:NoNewlineType w ellipses
     { List<LValue> lvs;
       if (a1 != null) lvs = FortressUtil.ids2Lvs(a2, a1, a3, false);
       else            lvs = FortressUtil.ids2Lvs(a2, a3, false);
       FortressUtil.validId(writer, lvs);
       yyValue = NodeFactory.makeVarDecl(writer, createSpan(yyStart,yyCount), lvs,
                                         Option.<Expr>none());
     }
   / a1:Mods? a2:BindIdOrBindIdTuple w colon w a3:NoNewlineTupleType
     { Span span = createSpan(yyStart,yyCount);
       List<Type> tys = a3.getElements();
       if (a2.size() != tys.size())
           log(span, "Mismatched numbers of variables and types.");
       List<LValue> lvs;
       if (a1 != null) lvs = FortressUtil.ids2Lvs(a2, a1, tys, false);
       else            lvs = FortressUtil.ids2Lvs(a2, tys, false);
       FortressUtil.validId(writer, lvs);
       yyValue = NodeFactory.makeVarDecl(writer, span, lvs, Option.<Expr>none());
     };
