/*******************************************************************************
    Copyright 2007 Sun Microsystems, Inc.,
    4150 Network Circle, Santa Clara, California 95054, U.S.A.
    All rights reserved.

    U.S. Government Rights - Commercial software.
    Government users are subject to the Sun Microsystems, Inc. standard
    license agreement and applicable provisions of the FAR and its supplements.

    Use is subject to license terms.

    This distribution may include materials developed by third parties.

    Sun, Sun Microsystems, the Sun logo and Java are trademarks or registered
    trademarks of Sun Microsystems, Inc. in the U.S. and other countries.
 ******************************************************************************/

/*
 * Definition of Fortress parameters.
 */
module Parameter(Header, Type, Expr, Identifier, Keyword, Symbol, Spacing);

import Header;
import Type;
import Expr;
import Identifier;
import Keyword;
import Symbol;
import Spacing;

/* ValParam ::= BindId | ( (w Params)? w ) */
List<Param> ValParam =
     a1:BindId
     { yyValue = FortressUtil.<Param, NormalParam>mkList(NodeFactory.makeParam(a1)); }
   / openparen a1:(w Params)? w closeparen
     { if (a1 == null) yyValue = FortressUtil.emptyParams();
       else            yyValue = a1;
     };

/* Params ::=
     (Param w , w)* (Varargs w , w)? Keyword (w , w Keyword)*
   | (Param w , w)*  Varargs
   |  Param (w , w Param)*
 */
List<Param> Params =
     a1s:(Param w comma w)* a2:(Varargs w comma w)? a3:Keyword
       a4s:(w comma w Keyword)*
     { yyValue = FortressUtil.<Param, NormalParam>mkList(a1s.list());
       if (a2 != null) yyValue.add(a2);
       yyValue.add(a3);
       yyValue.addAll(a4s.list());
     }
   / a1s:(Param w comma w)* a2:Varargs
     { yyValue = FortressUtil.<Param, NormalParam>mkList(a1s.list(), a2); }
   /  a1:Param a2s:(w comma w Param)*
     { yyValue = FortressUtil.<Param, NormalParam>mkList(a1, a2s.list()); };

/* Varargs ::= BindId w : w Type w ... */
VarargsParam VarargsParam = a1:BindId w colon w a2:Type w ellipses
     { VarargsType ty = new VarargsType(a2.getSpan(), a2);
       yyValue = NodeFactory.makeVarargsParam(NodeFactory.makeIdName(a1), ty);
     };
VarargsParam Varargs = VarargsParam ;

/* Keyword ::= Param w = w Expr */
Param Keyword = a1:Param w equals w a2:Expr
     { yyValue = NodeFactory.makeParam(a1, a2); };

/* Param ::= BindId (w IsType)? | Type */
NormalParam PlainParam =
     a1:BindId w a2:IsType
     { yyValue = NodeFactory.makeParam(a1, a2); }
   / !(BindId (w closedoublesquare / w closesquare /
               w opendoublesquare / w opensquare / w rightarrow))
     a1:BindId
     { yyValue = NodeFactory.makeParam(a1); }
   / a1:Type
     { yyValue = NodeFactory.makeParam(new Id(createSpan(yyStart,yyCount), "_"),
                                       a1); };
NormalParam Param = PlainParam ;

/* OpHeaderFront ::=
     opr (w StaticParams)? (w BIG)? w (LeftEncloser | Encloser) w Params w
     (RightEncloser | Encloser)
   | opr (w StaticParams)? w ValParam w (Op | ExponentOp)
   | opr (w BIG)? w (Op | caret | Encloser) (w StaticParams)? w ValParam
 */
FnHeaderFront OpHeaderFront =
     <Enclosing>
     opr opa1:(w StaticParams)? big:(w BIG)? w opa2:(LeftEncloser / Encloser) w
     opa3:Params w opa4:(RightEncloser / Encloser)
     { SimpleName name;
       Span span = createSpan(yyStart,yyCount);
       String left  = opa2.getText();
       String right = opa4.getText();
       if (PrecedenceMap.ONLY.matchedBrackets(left, right)) {
           if (big != null) {
               opa2 = NodeFactory.makeOp(opa2.getSpan(), "BIG " + left);
               opa4 = NodeFactory.makeOp(opa4.getSpan(), "BIG " + right);
           }
           name = new Bracketing(span, opa2, opa4);
       } else
           name = error(opa2, "Mismatched enclosing operator definition: "
                              + left + " and " + right);
       if (opa1 == null) opa1 = FortressUtil.emptyStaticParams();
       yyValue = new FnHeaderFront(name, opa1, opa3);
     }
   / opr a1:(w StaticParams)? w a2:ValParam w a3:(Op / ExponentOp)
     { Span span = createSpan(yyStart,yyCount);
       if (a1 == null) yyValue = new FnHeaderFront(new PostFix(span,a3), a2);
       else            yyValue = new FnHeaderFront(new PostFix(span,a3), a1, a2);
     }
   / opr big:(w BIG)? w
     a1:(Op / caret {yyValue=NodeFactory.makeOp(createSpan(yyStart,yyCount),"^");} / Encloser)
     a2:(w StaticParams)? w a3:ValParam
     { Span span = createSpan(yyStart,yyCount);
       Op op = (Op)a1;
       if (big != null)
           op = NodeFactory.makeOp(op.getSpan(), "BIG " + op.getText());
       if (a2 == null) yyValue = new FnHeaderFront(new Opr(span,op), a3);
       else            yyValue = new FnHeaderFront(new Opr(span,op), a2, a3);
     };
