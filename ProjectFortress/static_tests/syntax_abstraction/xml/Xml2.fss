(*******************************************************************************
    Copyright 2008 Sun Microsystems, Inc.,
    4150 Network Circle, Santa Clara, California 95054, U.S.A.
    All rights reserved.

    U.S. Government Rights - Commercial software.
    Government users are subject to the Sun Microsystems, Inc. standard
    license agreement and applicable provisions of the FAR and its supplements.

    Use is subject to license terms.

    This distribution may include materials developed by third parties.

    Sun, Sun Microsystems, the Sun logo and Java are trademarks or registered
    trademarks of Sun Microsystems, Inc. in the U.S. and other countries.
 ******************************************************************************)

component Xml2

  import FortressAst.{...}
  import List.{...}

  export Xml2

  trait Content
    hasElements():Boolean = false
    toString():String
  end

  object Element(info:Header, content_:List[\Content\], endTag:String) extends Content 
    toString():String = 
      do
        if info.getTag() =/= endTag then
          println "Tags dont match start '" info.getTag() "' end '" endTag "'"
        end
        var s:String = "<" info.getTag() (BIG STRING info.attributes()) ">"
        for x <- seq(content_) do 
          s := s x.toString() 
        end
        s := s "</" endTag ">"
        s
      end

    children():List[\Element\] = 
        content_.map[\Element\](fn x => cast[\Element\](x))
    
    attributes():List[\Attribute\] = info.attributes()
    
    hasElements():Boolean = content_.isEmpty() OR: (isElement(content_[0]))
    content():CData = cast[\CData\](content_[0])
    (*
    hasElements():Boolean = false
    *)

    isElement(e):Boolean =
        typecase e of
            Element => true
            else => false
        end
    
    tag():String = info.getTag()
  end
  Element(info:Header) = Element(info, emptyList[\Content\](), info.getTag())
  Element(info:Header, endTag:String) = Element(info, emptyList[\Content\](), endTag)

  object Header(startTag:String, attributes_:List[\Attribute\])
    getter getTag():String = startTag
    getter attributes():List[\Attribute\] = attributes_
  end

  object Attribute(key:String, val:String)
    getter getKey():String = key
    getter getValue():String = val
    getter toString():String = " " getKey() "='" getValue() "'"
  end

  object CData(v:String) extends Content
    toString():String = v
  end

end
