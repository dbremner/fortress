(*******************************************************************************
    Copyright 2009 Sun Microsystems, Inc.,
    4150 Network Circle, Santa Clara, California 95054, U.S.A.
    All rights reserved.

    U.S. Government Rights - Commercial software.
    Government users are subject to the Sun Microsystems, Inc. standard
    license agreement and applicable provisions of the FAR and its supplements.

    Use is subject to license terms.

    This distribution may include materials developed by third parties.

    Sun, Sun Microsystems, the Sun logo and Java are trademarks or registered
    trademarks of Sun Microsystems, Inc. in the U.S. and other countries.
 ******************************************************************************)

component LogFib
export Executable

(** Log-time fibonacci using only integer arithmetic.
    This is "faster" than repeated addition!
 **)

fib(n: ZZ): ZZ = do
    (fib_n1, fib_n) = logFib(n)
    fib_n
  end

logFib(n: ZZ): (ZZ, ZZ) =
    if n < 2 then
        (1-n, n)
    else
        h = n DIV 2
        (fib_h1, fib_h) = logFib(h)
        fib_n1 = fib_h1^2 + fib_h^2
        fib_n = fib_h (2 fib_h1 + fib_h)
        if even n then
            (fib_n1, fib_n)
        else
            (fib_n, fib_n1 + fib_n)
        end
    end

run(): () = do
    println(fib big(i)), i <- seq(0:100)
  end

end

