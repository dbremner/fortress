(*******************************************************************************
    Copyright 2008 Sun Microsystems, Inc.,
    4150 Network Circle, Santa Clara, California 95054, U.S.A.
    All rights reserved.

    U.S. Government Rights - Commercial software.
    Government users are subject to the Sun Microsystems, Inc. standard
    license agreement and applicable provisions of the FAR and its supplements.

    Use is subject to license terms.

    This distribution may include materials developed by third parties.

    Sun, Sun Microsystems, the Sun logo and Java are trademarks or registered
    trademarks of Sun Microsystems, Inc. in the U.S. and other countries.
 ******************************************************************************)

component wordcount
import CaseInsensitiveString.{...}
import File.{...}
import FileSupport.{...}
import List.{...}
import Map.{...}

export Executable

delimiters : List[\Char\] = <| ' ',  ',', '.', '?', '*', '\t', '-', '/', '[', ']' |>
delimMap : Map[\Char,Boolean\] = { c |-> true | c <- delimiters }
isDelimiter(c:Char):Boolean = delimMap.member(c,false)

maybeAddWord(word:String,
             database:Map[\CaseInsensitiveString,ZZ32\]):Map[\CaseInsensitiveString,ZZ32\]=
    database.updateWith(fn (mx) => Just[\ZZ32\](if x <- mx then x+1 else 1 end),
                        CaseInsensitiveString(word))

getWords(s:String):Map[\CaseInsensitiveString, ZZ32\] = do
    var database:Map[\CaseInsensitiveString,ZZ32\] := mapping[\CaseInsensitiveString,ZZ32\]()
    var count:ZZ32 := 1
    var current:ZZ32 := 0
    var start:ZZ32 := 0

    while (current < s.size()) do
        if isDelimiter(s[current]) then
           count := count - 1
           if count >= 1 then
               word:String = s[start#count]
               database := maybeAddWord(word, database)
          end
           start := current + 1
           count := 0
        end
        count += 1
        current += 1
    end
    count := count - 1
    if count >= 1 then
        word:String = s[start#count]
        database := maybeAddWord(word,database)
    end
    database
end

makeInv(x:String,y:ZZ32):Map[\ZZ32, List[\String\]\] = do
    var database:Map[\ZZ32,List[\String\]\] := mapping[\ZZ32,List[\String\]\]()
    database := database.add(y, <| x |>)
    database
end

opr UNIONSUM(a:Map[\CaseInsensitiveString,ZZ32\],
             b:Map[\CaseInsensitiveString,ZZ32\]):Map[\CaseInsensitiveString,ZZ32\] =
        a.union(fn(_,x,y)=>x+y, b)

opr BIG UNIONSUM(): BigReduction[\Map[\CaseInsensitiveString,ZZ32\],
                                 Map[\CaseInsensitiveString,ZZ32\]\] =
    embiggen[\Map[\CaseInsensitiveString,ZZ32\]\](
        fn (a,b) => a UNIONSUM b, {[\CaseInsensitiveString,ZZ32\]})

opr UNIONUNION(a:Map[\ZZ32,List[\String\]\], b:Map[\ZZ32, List[\String\]\]):Map[\ZZ32,List[\String\]\] =
    a.union(fn(k,x,y) =>x.append(y),b)

opr BIG UNIONUNION(): BigReduction[\Map[\ZZ32, List[\String\]\],Map[\ZZ32, List[\String\]\]\] =
    embiggen[\Map[\ZZ32, List[\String\]\]\](fn(a,b) => a UNIONUNION b, {[\ZZ32,List[\String\]\]})

processFile(name:String):() = do
    println("Processing file " name)
    var rs:FileReadStream = FileReadStream(name)
    database:Map[\CaseInsensitiveString, ZZ32\] =
        BIG UNIONSUM [l<-rs.lines()] getWords(l)
    rs.close()
    var invDatabase:Map[\ZZ32,List[\String\]\] =
        BIG UNIONUNION [(x,y) <-database] { y |-> <| x.toString() |> asif List[\String\] }
    try
        for i <- seq(1#100) do
            (c:ZZ32,m:List[\String\]) = invDatabase.maximum().get()
            invDatabase := invDatabase.deleteMaximum()
            println(m ": " c " times")
        end
    catch e
            NotFound => println("end")
    end
end


run(args:String...) = do
    processFile(getEnvironment("fortress.autohome", ".") "/ProjectFortress/demos/hamlet")
end
end
