(*******************************************************************************
    Copyright 2008 Sun Microsystems, Inc.,
    4150 Network Circle, Santa Clara, California 95054, U.S.A.
    All rights reserved.

    U.S. Government Rights - Commercial software.
    Government users are subject to the Sun Microsystems, Inc. standard
    license agreement and applicable provisions of the FAR and its supplements.

    Use is subject to license terms.

    This distribution may include materials developed by third parties.

    Sun, Sun Microsystems, the Sun logo and Java are trademarks or registered
    trademarks of Sun Microsystems, Inc. in the U.S. and other countries.
 ******************************************************************************)

native component TypeReflect
export TypeReflect

language="java"
package="com.sun.fortress.interpreter.glue.prim"

(** Reify a type parameter as a parametrically-typed object.
 *  Compute joins of reified type parameters, allowing
 *  us to simulate covariance.  Permit comparison with the usual partial
 *  ordering on types.
 *)
trait ReflectedType extends StandardPartialOrder[\ReflectedType\]
  (* comprises { Reflect[\T\] } where [\T\] *)
  getter toString(): String
  opr CMP(self, other:ReflectedType): Comparison =
      if self=other then EqualTo
      else
          j = joinReflected(self,other)
          if self=j then GreaterThan
          elif other=j then LessThan
          else Unordered
          end
      end
  opr =(self, other:ReflectedType): Boolean = false
end

value object Reflect[\T\]() extends ReflectedType
    getter toString(): String =
        builtinPrimitive("com.sun.fortress.interpreter.glue.prim.Reflect$ToString")
    opr =(self, other:ReflectedType): Boolean =
        typecase other of
            Reflect[\T\] => true
            else => false
        end
end

joinReflected(ty:ReflectedType, tys:ReflectedType...):() =
    builtinPrimitive("com.sun.fortress.interpreter.glue.prim.Reflect$Join")

reflectType[\T\](_:T):Reflect[\T\] = Reflect[\T\]()

end
