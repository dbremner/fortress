(*******************************************************************************
    Copyright 2008 Sun Microsystems, Inc.,
    4150 Network Circle, Santa Clara, California 95054, U.S.A.
    All rights reserved.

    U.S. Government Rights - Commercial software.
    Government users are subject to the Sun Microsystems, Inc. standard
    license agreement and applicable provisions of the FAR and its supplements.

    Use is subject to license terms.

    This distribution may include materials developed by third parties.

    Sun, Sun Microsystems, the Sun logo and Java are trademarks or registered
    trademarks of Sun Microsystems, Inc. in the U.S. and other countries.
 ******************************************************************************)

component RangeTest
import ArrayList.{...}
export Executable

chkOrder[\T\](a:Range[\T\], b:Range[\T\], expect: Comparison): () = do
    assert(a CMP b, expect, "Comparing ", a, " and ", b)
    assert(b CMP a, INVERSE expect, "InvComp ", a, " and ", b)
    typecase expect of
        LessThan =>
            assert(a < b, "Not <")
            assert(a <= b, "Not <=")
            assert(NOT (a > b), ">")
            assert(NOT (a >= b), ">=")
            assert(NOT (a = b), "< =")
            assert(b > a, "< Not >")
            assert(b >= a, "< Not >=")
            assert(NOT (b < a), "< <")
            assert(NOT (b <= a), "< <=")
            assert(NOT (b = a), "< ='")
            assert(b[a],a,"< indexing")
        EqualTo =>
            assert(a = b, "Not =")
            assert(a <= b, "= Not <=")
            assert(a >= b, "= Not >=")
            assert(NOT (a < b), "= <")
            assert(NOT (a > b), "= >")
            assert(b = a, "Not ='")
            assert(b >= a, "= Not >=")
            assert(b <= a, "= Not <=")
            assert(NOT (b > a), "= >'")
            assert(NOT (b < a), "= <'")
            assert(b[a],a,"= indexing")
        GreaterThan =>
            assert(a > b, "Not >")
            assert(a >= b, "Not >=")
            assert(NOT (a < b), "<")
            assert(NOT (a = b), "> =")
            assert(b < a, "> Not <")
            assert(b <= a, "> Not <=")
            assert(NOT (b > a), "> >")
            assert(NOT (b = a), "> ='")
            assert(a[b],b,"> indexing")
        Unordered =>
            assert(NOT (a = b), "U =")
            assert(NOT (a < b), "U <")
            assert(NOT (a <= b), "U <=")
            assert(NOT (a > b), "U >")
            assert(NOT (a >= b), "U >=")
            assert(NOT (b = a), "U ='")
            assert(NOT (b < a), "U <'")
            assert(NOT (b <= a), "U <='")
            assert(NOT (b > a), "U >'")
            assert(NOT (b >= a), "U >='")
      end
  end

run(args:String...):() = do
    mt : Range[\ZZ32\] = OpenRange[\ZZ32\]()
    llo : Range[\ZZ32\] = 0:
    hlo : Range[\ZZ32\] = 5:
    lsz : Range[\ZZ32\] = #3
    hsz : Range[\ZZ32\] = #7
    lhi : Range[\ZZ32\] = UpperRange[\ZZ32\](4)
    hhi : Range[\ZZ32\] = UpperRange[\ZZ32\](8)
    ll  : Range[\ZZ32\] = 0:3
    lm  : Range[\ZZ32\] = 0:7
    lh  : Range[\ZZ32\] = 0:9
    mm  : Range[\ZZ32\] = 6:7
    mh  : Range[\ZZ32\] = 6:10
    hh  : Range[\ZZ32\] = 8:10
    chkOrder(mt,mt,EqualTo)
    for i <- <|llo, hlo, lsz, hsz, lhi, hhi, ll, lm, lh, mm, mh, hh|> do
        chkOrder(mt,i,GreaterThan)
        chkOrder(i,i,EqualTo)
    end
    chkOrder(llo,hlo,GreaterThan)
    for i <- <|lsz,hsz,lhi,hhi|> do
        chkOrder(llo,i,Unordered)
        chkOrder(hlo,i,Unordered)
    end
    for i <- <|ll,lm,lh|> do
        chkOrder(llo,i,GreaterThan)
        chkOrder(hlo,i,Unordered)
    end
    for i <- <|mm,mh,hh|> do
        chkOrder(llo,i,GreaterThan)
        chkOrder(hlo,i,GreaterThan)
    end
    chkOrder(lsz,hsz,LessThan)
    for i <- <|lhi,hhi,ll,lm,lh,mm,mh,hh|> do
        chkOrder(lsz,i,Unordered)
        chkOrder(hsz,i,Unordered)
    end
    chkOrder(lhi,hhi,LessThan)
    chkOrder(lhi,ll,GreaterThan)
    chkOrder(hhi,ll,GreaterThan)
    for i <- <|lm,mm|> do
        chkOrder(lhi,i,Unordered)
        chkOrder(hhi,i,GreaterThan)
    end
    for i <- <|lh,mh,hh|> do
        chkOrder(lhi,i,Unordered)
        chkOrder(hhi,i,Unordered)
    end
    chkOrder(ll,lm,LessThan)
    chkOrder(ll,lh,LessThan)
    chkOrder(ll,i,Unordered), i <- <|mm,mh,hh|>
    chkOrder(lm,lh,LessThan)
    chkOrder(lm,i,Unordered), i <- <|mh,hh|>
    chkOrder(lh,i,Unordered), i <- <|mh,hh|>
    chkOrder(mm,i,LessThan), i <- <|lm,lh,mh|>
    chkOrder(mm,hh,Unordered)
  end

end
