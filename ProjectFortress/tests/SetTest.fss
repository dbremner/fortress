(*******************************************************************************
    Copyright 2008 Sun Microsystems, Inc.,
    4150 Network Circle, Santa Clara, California 95054, U.S.A.
    All rights reserved.

    U.S. Government Rights - Commercial software.
    Government users are subject to the Sun Microsystems, Inc. standard
    license agreement and applicable provisions of the FAR and its supplements.

    Use is subject to license terms.

    This distribution may include materials developed by third parties.

    Sun, Sun Microsystems, the Sun logo and Java are trademarks or registered
    trademarks of Sun Microsystems, Inc. in the U.S. and other countries.
 ******************************************************************************)

component SetTest
import Set.{...}
export Executable

  assertStr(a: Set[\ZZ32\], b:Set[\ZZ32\], s:String) =
  if a.toString() =/= b.toString() then
    fail(a "=/=" b ": " s)
  end

  run(args:String...):() = do
    a: Set[\ZZ32\] = set[\ZZ32\]()
    b: Set[\ZZ32\] = set[\ZZ32\](1#10)
    c: Set[\ZZ32\] = { 1 1, 2, 3, 4, 5, 6, 7, 8, 9, 10 }
    d: Set[\ZZ32\] = { e | i <- 1#5, e <- {2 i, 2 i - 1} }

    for (i,v) <- d.indexValuePairs() do
        assert(i+1,v," indexValuePairs wrong for ",d)
    end

    assertStr(b,c,"sets 1#10 are different")
    assertStr(c,d,"set 1#10 and comprehension are different")
    assert(BIG AND[x<-a] false, true, "nonempty? ", a)
    assert(BIG OR[x<-b] true, true, "empty? ", b)
    assert(17 NOTIN b, true, "17 IN ",b)
    assert(BIG AND[x<-1#10] x NOTIN a, true, "something in ",a)
    assert(BIG AND[x<-1#10] x IN b, true, "something not in ",b)
    assertStr(a[:], set[\ZZ32\](), "subscript operator[:] incorrect for empty set")
    assertStr(b[:], b, "subscript operator[:] incorrect for tree set")
    assertStr(b[5:], set[\ZZ32\](6#5), "subscript operator[x:] incorrect for tree set")
    assertStr(b[9:9], singleton[\ZZ32\](10), "subscript operator[y:y] incorrect for tree set")
  end

end
